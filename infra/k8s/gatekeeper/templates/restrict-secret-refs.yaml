apiVersion: templates.gatekeeper.sh/v1beta1
kind: ConstraintTemplate
metadata:
  name: k8srestrictsecretrefs
spec:
  crd:
    spec:
      names:
        kind: K8sRestrictSecretRefs
      validation:
        openAPIV3Schema:
          type: object
          properties:
            allowedServiceAccounts:
              type: array
              items:
                type: string
  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package k8srestrictsecretrefs

        default has_secret_ref = false

        has_secret_ref {
          v := input.review.object.spec.volumes[_]
          v.secret.secretName
        }

        has_secret_ref {
          c := all_containers[_]
          env := c.env[_]
          env.valueFrom.secretKeyRef.name
        }

        has_secret_ref {
          c := all_containers[_]
          envfrom := c.envFrom[_]
          envfrom.secretRef.name
        }

        all_containers[c] {
          c := input.review.object.spec.containers[_]
        }

        all_containers[c] {
          c := input.review.object.spec.initContainers[_]
        }

        all_containers[c] {
          c := input.review.object.spec.ephemeralContainers[_]
        }

        service_account_name := sa {
          sa := input.review.object.spec.serviceAccountName
          sa != ""
        }

        service_account_name := "default" {
          not input.review.object.spec.serviceAccountName
        }

        service_account_name := "default" {
          input.review.object.spec.serviceAccountName == ""
        }

        allowed_sa[sa] {
          sa := input.parameters.allowedServiceAccounts[_]
        }

        violation[{"msg": msg}] {
          has_secret_ref
          not allowed_sa[service_account_name]
          msg := sprintf("secret refs restricted to service accounts %v (found %s)", [input.parameters.allowedServiceAccounts, service_account_name])
        }
